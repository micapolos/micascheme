(library (leo2 untyped)
  (export untyped)
  (import
    (leo2 base)
    (leo2 term))

  (define (untyped $term)
    (term-switch $term
      ((nothing? $nothing)
        (throw untyped $nothing))
      ((anything? $anything)
        (throw untyped $anything))
      ((type? $type)
        (throw untyped $type))
      ((symbol? $symbol)
        (throw untyped $symbol))
      ((indexed? $indexed)
        (untyped (indexed-ref $indexed)))
      ((symbolic? $symbolic)
        (untyped (symbolic-ref $symbolic)))
      ((native? $native)
        $native)
      ((native-application? $native-application)
        (native-application
          (native-application-procedure $native-application)
          (map untyped (native-application-args $native-application))))
      ((variable? $variable)
        $variable)
      ((abstraction? $abstraction)
        (abstraction
          (procedure-untyped (abstraction-procedure $abstraction))))
      ((signature? $signature)
        (throw untyped $signature))
      ((application? $application)
        (application
          (untyped (application-lhs $application))
          (untyped (application-rhs $application))))
      ((branch? $branch)
        (branch
          (untyped (branch-condition $branch))
          (untyped (branch-consequent $branch))
          (untyped (branch-alternate $branch))))
      ((recursion? $recursion)
        (recursion
          (procedure-untyped (recursion-procedure $recursion))))
      ((annotated? $annotated)
        (untyped (annotated-ref $annotated)))
      ((evaluated? $evaluated)
        (untyped (evaluated-ref $evaluated)))
      ((typed? $typed)
        (untyped (typed-ref $typed)))))

  (define (procedure-untyped $procedure)
    (lambda (x) (untyped ($procedure x))))
)
