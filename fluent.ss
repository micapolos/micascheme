(library (fluent)
  (export fluent also)
  (import (scheme) (syntax) (procedure))

  (define-aux-keyword also)

  (define-syntax (fluent $syntax)
    (syntax-case $syntax (let also)
      ((fluent)
        #'(void))
      ((fluent expr)
        #'expr)
      ((fluent expr (let var with-expr) rest ...)
        (identifier? #'var)
        #'(let ((var expr))
          (fluent with-expr rest ...)))
      ((fluent expr (let (var ...) with-expr) rest ...)
        (for-all identifier? (syntaxes var ...))
        #'(let-values (((var ...) expr))
          (fluent with-expr rest ...)))
      ((fluent (arity expr) (also (fn arg ...)) rest ...)
        (and (integer? (datum arity)) (nonnegative? (datum arity)))
        (let (($tmps (generate-temporaries (iota (datum arity)))))
          #`(let-values (((#,@$tmps) expr))
            (fluent (arity (let () (fn #,@$tmps arg ...) (values #,@$tmps))) rest ...))))
      ((fluent (arity expr) (fn arg ...) rest ...)
        (and (integer? (datum arity)) (nonnegative? (datum arity)))
        (let (($tmps (generate-temporaries (iota (datum arity)))))
          #`(let-values (((#,@$tmps) expr))
            (fluent (fn #,@$tmps arg ...) rest ...))))
      ((fluent expr (also (fn arg ...)) rest ...)
        #'(let ((var expr))
          (fluent (let () (fn var arg ...) var) rest ...)))
      ((fluent expr (fn arg ...) rest ...)
        #'(fluent (fn expr arg ...) rest ...)))))
